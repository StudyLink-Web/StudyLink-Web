plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.9'
	id 'io.spring.dependency-management' version '1.1.7'
}



group = 'com.StudyLink'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// ===== Spring Boot Core =====
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	// ===== Spring Security & OAuth2 =====
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'

	// ===== Thymeleaf =====
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'

	// ===== Database =====
	implementation 'com.mysql:mysql-connector-j:8.0.33'  // 최신 MySQL 드라이버
	implementation 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:1.16'

	// ===== MongoDB =====
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'

// ===== QueryDSL =====
	implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
	implementation 'com.querydsl:querydsl-core:5.0.0'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
	annotationProcessor 'jakarta.annotation:jakarta.annotation-api'

	// ===== JWT =====
	implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

	// ===== Image Processing =====
	implementation 'net.coobird:thumbnailator:0.4.20'

	// ===== Lombok =====
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// ===== Development & Testing =====
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.springframework.security:spring-security-test'

	// ===== Firebase Admin SDK =====
	implementation 'com.google.firebase:firebase-admin:9.2.0'
}

tasks.named('test') {
	useJUnitPlatform()
}

// ===== QueryDSL QType 생성 설정 =====
def querydslSrcDir = 'src/main/generated'
clean {
	delete file(querydslSrcDir)
}
tasks.withType(JavaCompile) {
	options.generatedSourceOutputDirectory = file(querydslSrcDir)
	options.compilerArgs << '-parameters'
}

// ===== React Build Automation =====
def frontendDir = "$projectDir/src/main/frontend"

task installFrontend(type: Exec) {
	workingDir "$frontendDir"
	inputs.file("$frontendDir/package.json")
	outputs.dir("$frontendDir/node_modules")
	group = 'frontend'

	if (System.getProperty('os.name').toLowerCase().contains('windows')) {
		commandLine 'npm.cmd', 'install'
	} else {
		commandLine 'npm', 'install'
	}
}

task buildFrontend(type: Exec) {
	dependsOn installFrontend
	workingDir "$frontendDir"
	inputs.dir("$frontendDir/src")
	outputs.dir("$projectDir/src/main/resources/static/js")
	group = 'frontend'

	if (System.getProperty('os.name').toLowerCase().contains('windows')) {
		commandLine 'npm.cmd', 'run', 'build'
	} else {
		commandLine 'npm', 'run', 'build'
	}
}

// Spring Boot 리소스 처리 시 리액트 빌드를 먼저 수행하도록 함
// processResources.dependsOn buildFrontend

// ===== Spring Boot Main Class Configuration =====
springBoot {
	mainClass = 'com.StudyLink.www.WwwApplication'
}

